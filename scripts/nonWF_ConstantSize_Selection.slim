initialize()
{
	//if (exists("slimgui"))
	//{
	//defineConstant("Ne", 14000);
	//defineConstant("Mu", 1.53e-8);
	//defineConstant("Rho", 1.53e-7);
	//defineConstant("genomeSize", 2e6);
	//defineConstant("tractlen", 122000.0);
	//defineConstant("N_generations", 28000);
  //defineConstant("s_beneficial", 0.01);
  //defineConstant("s_deleterious", -0.01);
  //defineConstant("proportion_neutral", 0.9);
  //defineConstant("proportion_beneficial", 0.1);
  //defineConstant("proportion_deleterious", 0.0);
	//}


	initializeSLiMModelType("nonWF");
	//initializeTreeSeq(); // record the Tree for recapitation and/or adding neutral mutations later
	//initializeMutationRate(0); // no neutral mutation in the forward simulation, we will add them later with msprime.
  initializeMutationRate(Mu);

  initializeMutationType("m1", 1.0, "f", 0.0); // neutral (unused)
  initializeMutationType("m2",  1.0, "f", s_beneficial); // beneficial DFE
  initializeMutationType("m3",  1.0, "f", s_deleterious); // deleterious DFE

	//initializeGenomicElementType("g1", m1, 1.0);
  initializeGenomicElementType("g1", c(m1, m2, m3), c(proportion_neutral, proportion_beneficial, proportion_deleterious));
  //initializeGenomicElementType("g1", c(m1, m2, m3), c(0.6, 0.0, 0.4));

  initializeGenomicElement(g1, 0, genomeSize - 1);
  initializeRecombinationRate(0); // In SLiM recombination is between sister chromatids
  defineConstant("HGTrate", Rho * genomeSize); // HGT probability

}

1 early()
{
	sim.addSubpop("p1", Ne);
	sim.rescheduleScriptBlock(s1, start=N_generations, end=N_generations);
}

early()
{
	if (sim.generation % 1000 == 0)
	{
		cat(">> (" + sim.generation + ") GC : ");
		catn(sim.getValue("GC"));
	}
	sim.setValue("GC", 0);
}


// modify the reproduction callback
// to simulate homologuous recombination in a gene conversion manner
// after HGT of homologuous DNA fragments
// Modify the reproduction callback:
// Draw the number of target of a HGT event
// For each of them, draw a source individual, a start position
// and a size of the fragment to be recombined through GC
reproduction()
{
	// each parental individual reproduces twice, with independent probabilities of HGT
	parents = p1.individuals;

	for (rep in 0:1)
	{
		if (HGTrate > 0)
		{
			is_HGT = rbinom(size(parents), 1, HGTrate);
		}
		else
		{
			is_HGT = integer(size(parents)); // empty vector
		}
		for (i in seqAlong(parents))
		{
			if (is_HGT[i])
			{
				HGTsource = p1.sampleIndividuals(1, exclude=parents[i]).genome1;
				pos_beg = rdunif(1, 0, genomeSize - 1);
				tractLength = rgeom(1, 1.0 / tractlen);
				// HGT follows exponential distribution in Garud and Good, 2019
				//tractLength = asInteger(round(rexp(1, 1.0 / tractlen)));
				pos_end = pos_beg + tractLength - 1;
				if (pos_end == -1) { // if pos_beg and tractLength are both 0
					pos_end = 1;     // prevent the next mod operation to be negative which would crash later
				}
				else
				{
					pos_end = integerMod(pos_beg + tractLength - 1, genomeSize);
				}
				// HGT from breaks[0] forward to breaks[1] on a circular chromosome
				if (pos_beg > pos_end)
					breaks = c(0, pos_end, pos_beg);
				else
					breaks = c(pos_beg, pos_end);

				subpop.addRecombinant(parents[i].genome1, HGTsource, breaks, NULL, NULL, NULL);
			}
			else
			{
				// no horizontal gene transfer; clonal replication
				subpop.addRecombinant(parents[i].genome1, NULL, NULL, NULL, NULL, NULL);
			}
		}
		sim.setValue("GC", sim.getValue("GC")+sum(is_HGT));
	}

	self.active = 0;
}

early()
{
	// get output before you kill off parents at the final generation
	if (sim.generation == N_generations)
	{
	sim.outputFull(filePath=outputFull_path);

	}

	inds = p1.individuals;
	ages = inds.age;

	// kill off parental individuals; conceptually they don't even exist,
	// since they split by mitosis to generate their offspring
	inds[ages > 0].fitnessScaling = 0.0;

	// density-dependent population regulation on juveniles, toward Ne
	juvenileCount = sum(ages == 0);
	inds[ages == 0].fitnessScaling = Ne / juvenileCount;
}

// end of the simulation
s1 late()
{
	//sim.treeSeqOutput("/Users/wrshoemaker/GitHub/slim_recipes/scripts");
  //sim.outputFull(filePath=outputFull_path);
	sim.simulationFinished();
}
